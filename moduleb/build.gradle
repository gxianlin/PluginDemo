if (rootProject.ext.isApplication) {
    //当是一个独立项目时，项目类型为application，反之是library
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        //当是一个独立项目时，需要包名
        if (rootProject.ext.isApplication) {
            applicationId "com.ilynn.moduleb"
        }
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //组件与组件之间的资源命名冲突
        //作用：在创建资源文件时命名会自动添加前缀
        resourcePrefix "moduleb_"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //
    sourceSets {
        main {
            //根据module类型加载不同的mainfest文件
            if (rootProject.ext.isApplication) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/release/AndroidManifest.xml'
                //移除多余资源
                java {
                    exclude 'debug/**'
                }
            }
        }
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile rootProject.ext.dependencies.supportv7
    compile rootProject.ext.dependencies.design
    compile rootProject.ext.dependencies.constraint
    testCompile 'junit:junit:4.12'
    compile project(':base')
}